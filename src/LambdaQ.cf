-- Documentation of LBNF Gramar: https://bnfc.readthedocs.io/en/latest/lbnf.html# --
-- Generating a language frontend using the BNF Converter: http://bnfc.digitalgrammars.com/ --

layout toplevel ;
layout "let" ;
layout stop "in" ;

entrypoints Program;

-- A program is a list of functions --
ProgDef .  Program ::= [FunctionDeclaration] ;

-- Types --
TypeBit    . Type3  ::= "Bit" ;
TypeQbit   . Type3  ::= "Qbit" ;
TypeUnit   . Type3  ::= "()" ;
TypeNonLin . Type2  ::= "!" Type3 ;
TypeExp    . Type1  ::= Type2 "**" Integer ;
TypeTensr  . Type1  ::= Type2 "*" Type1 ;      -- right-associative
TypeFunc   . Type   ::= Type1 "->" Type ;      -- right-associative
coercions Type 3 ;

-- Angle is a fraction of 2Ï€ --
AAngl . Angle ::= Double ;

-- Control States --
CStateZero   . ControlState ::= "@0" ;
CStateOne    . ControlState ::= "@1" ;
CStatePlus   . ControlState ::= "@+" ;
CStateMinus  . ControlState ::= "@-" ;
CStatePlusI  . ControlState ::= "@+i" ;
CStateMinusI . ControlState ::= "@-i" ;

-- Controls --
CCtrl . Control ::= Term ControlState;
separator nonempty Control "," ;

position token GateIdent (upper (letter | digit | '_')*) ;

GateH             . Gate ::= "gate" "H" Term;                                                    -- Hadamard Gate
GateHC            . Gate ::= "ctrl-gate" "H" Term "with" "[" [Control] "]" ;                     -- Hadamard Gate
GateX             . Gate ::= "gate" "X" Term ;                                                   -- Pauli X Gate
GateXC            . Gate ::= "ctrl-gate" "X" Term "with" "[" [Control] "]" ;                     -- Pauli X Gate
GateY             . Gate ::= "gate" "Y" Term ;                                                   -- Pauli Y Gate
GateYC            . Gate ::= "ctrl-gate" "Y" Term "with" "[" [Control] "]" ;                     -- Pauli Y Gate
GateZ             . Gate ::= "gate" "Z" Term ;                                                   -- Pauli Z Gate
GateZC            . Gate ::= "ctrl-gate" "Z" Term "with" "[" [Control] "]" ;                     -- Pauli Z Gate
GateI             . Gate ::= "gate" "ID" Term ;                                                  -- Identity Gate
GateXRt           . Gate ::= "gate" "ROOT_X" Integer Term ;                                      -- Root of Pauli X gate (root is specified as k in 1/2^k)
GateXRtC          . Gate ::= "ctrl-gate" "ROOT_X" Integer Term "with" "[" [Control] "]" ;        -- Root of Pauli X gate (root is specified as k in 1/2^k)
GateXRtDag        . Gate ::= "gate" "ROOT_X_DAG" Integer Term ;                                  -- Conjugate of Root of Pauli X gate (root is specified as k in 1/2^k)
GateXRtDagC       . Gate ::= "ctrl-gate" "ROOT_X_DAG" Integer Term "with" "[" [Control] "]" ;    -- Conjugate of Root of Pauli X gate (root is specified as k in 1/2^k)
GateYRt           . Gate ::= "gate" "ROOT_Y" Integer Term ;                                      -- Root of Pauli Y gate (root is specified as k in 1/2^k)
GateYRtC          . Gate ::= "ctrl-gate" "ROOT_Y" Integer Term "with" "[" [Control] "]" ;        -- Root of Pauli Y gate (root is specified as k in 1/2^k)
GateYRtDag        . Gate ::= "gate" "ROOT_Y_DAG" Integer Term ;                                  -- Conjugate of Root of Pauli Y gate (root is specified as k in 1/2^k)
GateYRtDagC       . Gate ::= "ctrl-gate" "ROOT_Y_DAG" Integer Term "with" "[" [Control] "]" ;    -- Conjugate of Root of Pauli Y gate (root is specified as k in 1/2^k)
GateZRt           . Gate ::= "gate" "ROOT_Z" Integer Term ;                                      -- Root of Pauli Z gate (root is specified as k in 1/2^k)
GateZRtC          . Gate ::= "ctrl-gate" "ROOT_Z" Integer Term "with" "[" [Control] "]" ;        -- Root of Pauli Z gate (root is specified as k in 1/2^k)
GateZRtDag        . Gate ::= "gate" "ROOT_Z_DAG" Integer Term ;                                  -- Conjugate of Root of Pauli Z gate (root is specified as k in 1/2^k)
GateZRtDagC       . Gate ::= "ctrl-gate" "ROOT_Z_DAG" Integer Term "with" "[" [Control] "]" ;    -- Conjugate of Root of Pauli Z gate (root is specified as k in 1/2^k)
GateS             . Gate ::= "gate" "S" Term ;                                                   -- S gate: sqrt of Z
GateSC            . Gate ::= "ctrl-gate" "S" Term "with" "[" [Control] "]" ;                     -- S gate: sqrt of Z
GateSDag          . Gate ::= "gate" "S_DAG" Term ;                                               -- Conjugate of S gate
GateSDagC         . Gate ::= "ctrl-gate" "S_DAG" Term "with" "[" [Control] "]" ;                 -- Conjugate of S gate
GateT             . Gate ::= "gate" "T" Term ;                                                   -- T Gate: sqrt of S
GateTC            . Gate ::= "ctrl-gate" "T" Term "with" "[" [Control] "]" ;                     -- T Gate: sqrt of S
GateTDag          . Gate ::= "gate" "T_DAG" Term ;                                               -- Conjugate of T gate
GateTDagC         . Gate ::= "ctrl-gate" "T_DAG" Term "with" "[" [Control] "]" ;                 -- Conjugate of T gate
GateSqrtX         . Gate ::= "gate" "SQRT_X" Term ;                                              -- V gate: sqrt of X
GateSqrtXC        . Gate ::= "ctrl-gate" "SQRT_X" Term "with" "[" [Control] "]" ;                -- V gate: sqrt of X
GateSqrtXDag      . Gate ::= "gate" "SQRT_X_DAG" Term ;                                          -- Conjugate of V gate
GateSqrtXDagC     . Gate ::= "ctrl-gate" "SQRT_X_DAG" Term "with" "[" [Control] "]" ;            -- Conjugate of V gate
GateSqrtY         . Gate ::= "gate" "SQRT_Y" Term ;                                              -- h gate: sqrt of Y
GateSqrtYC        . Gate ::= "ctrl-gate" "SQRT_Y" Term "with" "[" [Control] "]" ;                -- h gate: sqrt of Y
GateSqrtYDag      . Gate ::= "gate" "SQRT_Y_DAG" Term ;                                          -- Conjugate of h gate
GateSqrtYDagC     . Gate ::= "ctrl-gate" "SQRT_Y_DAG" Term "with" "[" [Control] "]" ;            -- Conjugate of h gate
GateRxTheta       . Gate ::= "gate" "RX" Angle Term ;                                            -- Single parametric rotation around X axis on the Bloch sphere
GateRxThetaC      . Gate ::= "ctrl-gate" "RX" Angle Term "with" "[" [Control] "]" ;              -- Single parametric rotation around X axis on the Bloch sphere
GateRyTheta       . Gate ::= "gate" "RY" Angle Term ;                                            -- Single parametric rotation around Y axis on the Bloch sphere
GateRyThetaC      . Gate ::= "ctrl-gate" "RY" Angle Term "with" "[" [Control] "]" ;              -- Single parametric rotation around Y axis on the Bloch sphere
GateRzTheta       . Gate ::= "gate" "RZ" Angle Term ;                                            -- Single parametric rotation around Z axis on the Bloch sphere
GateRzThetaC      . Gate ::= "ctrl-gate" "RZ" Angle Term "with" "[" [Control] "]" ;              -- Single parametric rotation around Z axis on the Bloch sphere
GateU1            . Gate ::= "gate" "U1" Angle Term ;                                            -- One parametric generic gate
GateU1C           . Gate ::= "ctrl-gate" "U1" Angle Term "with" "[" [Control] "]" ;              -- One parametric generic gate
GateU2            . Gate ::= "gate" "U2" "(" Angle "," Angle ")" Term ;                          -- Two parametric generic gate
GateU2C           . Gate ::= "ctrl-gate" "U2" "(" Angle "," Angle ")" Term "with" "[" [Control] "]" ;           -- Two parametric generic gate
GateU3            . Gate ::= "gate" "U3" "(" Angle "," Angle "," Angle ")" Term ;                 -- Thee parametric generic gate
GateU3C           . Gate ::= "ctrl-gate" "U3" "(" Angle "," Angle "," Angle ")" Term "with" "[" [Control] "]" ;  -- Thee parametric generic gate
GateSwp           . Gate ::= "gate" "SWAP" Term ;                                                -- Swap gate
GateSwpC          . Gate ::= "ctrl-gate" "SWAP" Term "with" "[" [Control] "]" ;                  -- Swap gate
GateSqrtSwp       . Gate ::= "gate" "SQRT_SWAP" Term ;                                           -- sqrt of Swap gate
GateSqrtSwpC      . Gate ::= "ctrl-gate" "SQRT_SWAP" Term "with" "[" [Control] "]" ;             -- sqrt of Swap gate
GateSqrtSwpDag    . Gate ::= "gate" "SQRT_SWAP_DAG" Term ;                                       -- Conjugate of root of Swap gate
GateSqrtSwpDagC   . Gate ::= "ctrl-gate" "SQRT_SWAP_DAG" Term "with" "[" [Control] "]" ;         -- Conjugate of root of Swap gate
GateISwp          . Gate ::= "gate" "ISWAP" Term ;                                               -- ISwap gate
GateISwpC         . Gate ::= "ctrl-gate" "ISWAP" Term "with" "[" [Control] "]" ;                 -- ISwap gate
GateFSwp          . Gate ::= "gate" "FSWAP" Term ;                                               -- FSwap gate
GateFSwpC         . Gate ::= "ctrl-gate" "FSWAP" Term "with" "[" [Control] "]" ;                 -- FSwap gate
GateSwpTheta      . Gate ::= "gate" "SWAP_THETA" Term Angle ;                                    -- Swap theta gate
GateSwpThetaC     . Gate ::= "ctrl-gate" "SWAP_THETA" Angle Term"with" "[" [Control] "]"  ;      -- Swap theta gate
GateSwpRt         . Gate ::= "gate" "ROOT_SWAP" Term Integer ;                                   -- Root of Swap gate (root is specified as k in 1/2^k)
GateSwpRtC        . Gate ::= "ctrl-gate" "ROOT_SWAP" Integer Term "with" "[" [Control] "]" ;     -- Root of Swap gate (root is specified as k in 1/2^k)
GateSwpRtDag      . Gate ::= "gate" "ROOT_SWAP_DAG" Integer Term ;                               -- Conjugate of Root of Swap gate (root is specified as k in 1/2^k)
GateSwpRtDagC     . Gate ::= "ctrl-gate" "ROOT_SWAP_DAG" Integer Term "with" "[" [Control] "]" ; -- Conjugate of Root of Swap gate (root is specified as k in 1/2^k)
GateGeneric       . Gate ::= "gate" GateIdent Term ;                                             -- Generic gate
GateGenericC      . Gate ::= "ctrl-gate" GateIdent Term "with" "[" [Control] "]" ;               -- Generic gate

-- Names for Variables --
position token Var ((lower | '_') (letter | digit | '_' | '\'')*) ;

LetVar . LetVariable ::= Var ;
separator nonempty LetVariable "," ;

LambdaVar . LambdaVariable ::= Var ;
separator LambdaVariable " " ;

-- Declaring Tuples --
Tup . Tuple ::= "(" Term "," [Term] ")" ;
separator nonempty Term "," ;

-- Lambda token --
token Lambda '\\' ;

-- Bit --
position token Bit ('0' | '1') ;

-- Terms --
TVar   . Term3 ::= Var ;
TBit   . Term3 ::= Bit ;
TGate  . Term3 ::= Gate ;
TTup   . Term3 ::= Tuple ;
TUnit  . Term3 ::= "()" ;
TIfEl  . Term1 ::= "if" Term "then" Term "else" Term ;
TLet1  . Term1 ::= "let" "{" LetVariable "=" Term "}" "in" Term ;
TLet2  . Term1 ::= "let" "{" "(" LetVariable "," [LetVariable] ")" "=" Term "}" "in" Term ;
TCase  . Term1 ::= "case" Term "of" CaseExpression [ CaseExpression ] ;
TLmbd  . Term1 ::= Lambda LambdaVariable [LambdaVariable] "->" Term ;
TApp   . Term2 ::= Term2 Term3 ;      -- left-associative  --
TDollr . Term1 ::= Term2 "$" Term1 ;  -- right-associative --
coercions Term 3 ;

-- Case Expressions --
CaseExp . CaseExpression ::= Term "->" Var ;
separator nonempty CaseExpression " " ;

-- Function Arguments --
FunArg . Arg ::= Var ;
separator Arg " " ;

-- Function Definition --
FunDef . FunctionDefinition ::= Var [Arg] "=" Term ;
_      . FunctionDefinition ::= FunctionDefinition ";" ;   -- Semantic dummies: parser accepts extra semicolons

-- Type Definition --
FunType . FunctionType  ::= Var "::" Type ;
_       . FunctionType  ::=  FunctionType ";" ;            -- Semantic dummies: parser accepts extra semicolons

-- Function Declaration --
FunDecl . FunctionDeclaration ::= FunctionType ";" FunctionDefinition ";" ;
separator FunctionDeclaration "" ;

-- Format for specifying comments --
comment "--" ; 
comment "{-" "-}" ;


