-- Documentation of LBNF Gramar: https://bnfc.readthedocs.io/en/latest/lbnf.html# --
-- Generating a language frontend using the BNF Converter: http://bnfc.digitalgrammars.com/ --

layout toplevel ;
layout "let" ;
layout stop "in" ;

entrypoints Program;

-- A program is a list of functions --
ProgDef .  Program ::= [FunctionDeclaration] ;

-- Types --
TypeBit    . Type3  ::= "Bit" ;
TypeQbit   . Type3  ::= "Qbit" ;
TypeUnit   . Type3  ::= "()" ;
TypeNonLin . Type2  ::= "!" Type3 ;
TypeExp    . Type1  ::= Type2 "**" Integer ;
TypeTensr  . Type1  ::= Type2 "*" Type1 ;      -- right-associative
TypeFunc   . Type   ::= Type1 "->" Type ;      -- right-associative
coercions Type 3 ;

-- Angle is a fraction of 2Ï€ --
AAngl . Angle ::= Double ;

-- Control States --
CStateZero   . ControlState ::= "@0" ;
CStateOne    . ControlState ::= "@1" ;
CStatePlus   . ControlState ::= "@+" ;
CStateMinus  . ControlState ::= "@-" ;
CStatePlusI  . ControlState ::= "@+i" ;
CStateMinusI . ControlState ::= "@-i" ;

-- Controls --
CCtrl . Control ::= ControlState "->" Term ;
separator nonempty Control "," ;

position token GateIdent (upper (letter | digit | '_')*) ;

GateH             . Gate ::= "H" ;                                       -- Hadamard Gate
GateHC            . Gate ::= "H" "[" [Control] "]" ;                     -- Hadamard Gate
GateX             . Gate ::= "X" ;                                       -- Pauli X Gate
GateXC            . Gate ::= "X" "[" [Control] "]" ;                     -- Pauli X Gate
GateY             . Gate ::= "Y" ;                                       -- Pauli Y Gate
GateYC            . Gate ::= "Y" "[" [Control] "]" ;                     -- Pauli Y Gate
GateZ             . Gate ::= "Z" ;                                       -- Pauli Z Gate
GateZC            . Gate ::= "Z" "[" [Control] "]" ;                     -- Pauli Z Gate
GateI             . Gate ::= "ID" ;                                      -- Identity Gate
GateXRt           . Gate ::= "ROOT_X" Integer ;                          -- Root of Pauli X gate (root is specified as k in 1/2^k)
GateXRtC          . Gate ::= "ROOT_X" Integer "[" [Control] "]" ;        -- Root of Pauli X gate (root is specified as k in 1/2^k)
GateXRtDag        . Gate ::= "ROOT_X_DAG" Integer ;                      -- Conjugate of Root of Pauli X gate (root is specified as k in 1/2^k)
GateXRtDagC       . Gate ::= "ROOT_X_DAG" Integer "[" [Control] "]" ;    -- Conjugate of Root of Pauli X gate (root is specified as k in 1/2^k)
GateYRt           . Gate ::= "ROOT_Y" Integer ;                          -- Root of Pauli Y gate (root is specified as k in 1/2^k)
GateYRtC          . Gate ::= "ROOT_Y" Integer "[" [Control] "]" ;        -- Root of Pauli Y gate (root is specified as k in 1/2^k)
GateYRtDag        . Gate ::= "ROOT_Y_DAG" Integer ;                      -- Conjugate of Root of Pauli Y gate (root is specified as k in 1/2^k)
GateYRtDagC       . Gate ::= "ROOT_Y_DAG" Integer "[" [Control] "]" ;    -- Conjugate of Root of Pauli Y gate (root is specified as k in 1/2^k)
GateZRt           . Gate ::= "ROOT_Z" Integer ;                          -- Root of Pauli Z gate (root is specified as k in 1/2^k)
GateZRtC          . Gate ::= "ROOT_Z" Integer "[" [Control] "]" ;        -- Root of Pauli Z gate (root is specified as k in 1/2^k)
GateZRtDag        . Gate ::= "ROOT_Z_DAG" Integer ;                      -- Conjugate of Root of Pauli Z gate (root is specified as k in 1/2^k)
GateZRtDagC       . Gate ::= "ROOT_Z_DAG" Integer "[" [Control] "]" ;    -- Conjugate of Root of Pauli Z gate (root is specified as k in 1/2^k)
GateS             . Gate ::= "S" ;                                       -- S gate: sqrt of Z
GateSC            . Gate ::= "S" "[" [Control] "]" ;                     -- S gate: sqrt of Z
GateSDag          . Gate ::= "S_DAG" ;                                   -- Conjugate of S gate
GateSDagC         . Gate ::= "S_DAG" "[" [Control] "]" ;                 -- Conjugate of S gate
GateT             . Gate ::= "T" ;                                       -- T Gate: sqrt of S
GateTC            . Gate ::= "T" "[" [Control] "]" ;                     -- T Gate: sqrt of S
GateTDag          . Gate ::= "T_DAG" ;                                   -- Conjugate of T gate
GateTDagC         . Gate ::= "T_DAG" "[" [Control] "]" ;                 -- Conjugate of T gate
GateSqrtX         . Gate ::= "SQRT_X" ;                                  -- V gate: sqrt of X
GateSqrtXC        . Gate ::= "SQRT_X" "[" [Control] "]" ;                -- V gate: sqrt of X
GateSqrtXDag      . Gate ::= "SQRT_X_DAG" ;                              -- Conjugate of V gate
GateSqrtXDagC     . Gate ::= "SQRT_X_DAG" "[" [Control] "]" ;            -- Conjugate of V gate
GateSqrtY         . Gate ::= "SQRT_Y" ;                                  -- h gate: sqrt of Y
GateSqrtYC        . Gate ::= "SQRT_Y" "[" [Control] "]" ;                -- h gate: sqrt of Y
GateSqrtYDag      . Gate ::= "SQRT_Y_DAG" ;                              -- Conjugate of h gate
GateSqrtYDagC     . Gate ::= "SQRT_Y_DAG" "[" [Control] "]" ;            -- Conjugate of h gate
GateRxTheta       . Gate ::= "RX" Angle ;                                -- Single parametric rotation around X axis on the Bloch sphere
GateRxThetaC      . Gate ::= "RX" Angle "[" [Control] "]" ;              -- Single parametric rotation around X axis on the Bloch sphere
GateRyTheta       . Gate ::= "RY" Angle ;                                -- Single parametric rotation around Y axis on the Bloch sphere
GateRyThetaC      . Gate ::= "RY" Angle "[" [Control] "]" ;              -- Single parametric rotation around Y axis on the Bloch sphere
GateRzTheta       . Gate ::= "RZ" Angle ;                                -- Single parametric rotation around Z axis on the Bloch sphere
GateRzThetaC      . Gate ::= "RZ" Angle "[" [Control] "]" ;              -- Single parametric rotation around Z axis on the Bloch sphere
GateU1            . Gate ::= "U1" Angle ;                                -- One parametric generic gate
GateU1C           . Gate ::= "U1" Angle "[" [Control] "]" ;              -- One parametric generic gate
GateU2            . Gate ::= "U2" Angle Angle ;                          -- Two parametric generic gate
GateU2C           . Gate ::= "U2" Angle Angle "[" [Control] "]" ;        -- Two parametric generic gate
GateU3            . Gate ::= "U3" Angle Angle Angle ;                    -- Thee parametric generic gate
GateU3C           . Gate ::= "U3" Angle Angle Angle "[" [Control] "]" ;  -- Thee parametric generic gate
GateSwp           . Gate ::= "SWAP" ;                                    -- Swap gate
GateSwpC          . Gate ::= "SWAP" "[" [Control] "]" ;                  -- Swap gate
GateSqrtSwp       . Gate ::= "SQRT_SWAP" ;                               -- sqrt of Swap gate
GateSqrtSwpC      . Gate ::= "SQRT_SWAP" "[" [Control] "]" ;             -- sqrt of Swap gate
GateSqrtSwpDag    . Gate ::= "SQRT_SWAP_DAG" ;                           -- Conjugate of root of Swap gate
GateSqrtSwpDagC   . Gate ::= "SQRT_SWAP_DAG" "[" [Control] "]" ;         -- Conjugate of root of Swap gate
GateISwp          . Gate ::= "ISWAP" ;                                   -- ISwap gate
GateISwpC         . Gate ::= "ISWAP" "[" [Control] "]" ;                 -- ISwap gate
GateFSwp          . Gate ::= "FSWAP" ;                                   -- FSwap gate
GateFSwpC         . Gate ::= "FSWAP" "[" [Control] "]" ;                 -- FSwap gate
GateSwpTheta     . Gate ::= "SWAP_THETA" Angle ;                         -- Swap theta gate
GateSwpRt         . Gate ::= "ROOT_SWAP" Integer ;                       -- Root of Swap gate (root is specified as k in 1/2^k)
GateSwpRtC        . Gate ::= "ROOT_SWAP" Integer "[" [Control] "]" ;     -- Root of Swap gate (root is specified as k in 1/2^k)
GateSwpRtDag      . Gate ::= "ROOT_SWAP_DAG" Integer ;                   -- Conjugate of Root of Swap gate (root is specified as k in 1/2^k)
GateSwpRtDagC     . Gate ::= "ROOT_SWAP_DAG" Integer "[" [Control] "]" ; -- Conjugate of Root of Swap gate (root is specified as k in 1/2^k)
GateGeneric       . Gate ::= GateIdent ;                                 -- Generic gate
GateGenericC      . Gate ::= GateIdent "[" [Control] "]" ;               -- Generic gate

-- Names for Variables --
position token Var ((lower | '_') (letter | digit | '_' | '\'')*) ;

LetVar . LetVariable ::= Var ;
separator nonempty LetVariable "," ;

LambdaVar . LambdaVariable ::= Var ;
separator LambdaVariable " " ;

-- Declaring Tuples --
Tup . Tuple ::= "(" Term "," [Term] ")" ;
separator nonempty Term "," ;

-- Lambda token --
token Lambda '\\' ;

-- Bit --
position token Bit ('0' | '1') ;

-- Terms --
TVar   . Term3 ::= Var ;
TBit   . Term3 ::= Bit ;
TGate  . Term3 ::= Gate ;
TTup   . Term3 ::= Tuple ;
TUnit  . Term3 ::= "()" ;
TIfEl  . Term1 ::= "if" Term "then" Term "else" Term ;
TLet1  . Term1 ::= "let" "{" LetVariable "=" Term "}" "in" Term ;
TLet2  . Term1 ::= "let" "{" "(" LetVariable "," [LetVariable] ")" "=" Term "}" "in" Term ;
TCase  . Term1 ::= "case" Term "of" CaseExpression [ CaseExpression ] ;
TLmbd  . Term1 ::= Lambda LambdaVariable [LambdaVariable] "->" Term ;
TApp   . Term2 ::= Term2 Term3 ;      -- left-associative  --
TDollr . Term1 ::= Term2 "$" Term1 ;  -- right-associative --
coercions Term 3 ;

-- Case Expressions --
CaseExp . CaseExpression ::= Term "->" Var ;
separator nonempty CaseExpression " " ;

-- Function Arguments --
FunArg . Arg ::= Var ;
separator Arg " " ;

-- Function Definition --
FunDef . FunctionDefinition ::= Var [Arg] "=" Term ;
_      . FunctionDefinition ::= FunctionDefinition ";" ;   -- Semantic dummies: parser accepts extra semicolons

-- Type Definition --
FunType . FunctionType  ::= Var "::" Type ;
_       . FunctionType  ::=  FunctionType ";" ;            -- Semantic dummies: parser accepts extra semicolons

-- Function Declaration --
FunDecl . FunctionDeclaration ::= FunctionType ";" FunctionDefinition ";" ;
separator FunctionDeclaration "" ;

-- Format for specifying comments --
comment "--" ; 
comment "{-" "-}" ;


